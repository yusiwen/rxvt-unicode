#! perl

sub on_keyboard_command {
   my ($self, $cmd) = @_;

   $cmd eq "selection:rot13"
      and $self->selection (map { y/A-Za-z/N-ZA-Mn-za-m/; $_ } $self->selection);

   ()
}

# "find interetsing things"-patterns
my @mark_patterns = (
   # urls, just a heuristic
   qr{(
      (?:https?|ftp|news|mailto|file)://[ab-zA-Z0-9\-\@;\/?:&=%\$_.+!*\x27(),~]+
      [ab-zA-Z0-9\-\@;\/?:&=%\$_+!*\x27(),~]   # do not include a trailing dot, its wrong too often
   )}x,

   # common forms of quoting
   qr{(?:^|\s) [‘`] ([^‘`’']+) [’'] (?:\s|$)}x,

   # shell-like argument quoting
   qr{\G [\ \t|&;<>()] *(
      (?:
         [^\\"'\ \t|&;<>()]+
         | \\.
         | " (?: [^\\"]+ | \\. )* "
         | ' [^']* '
      )+
   )}x,
);

# "correct obvious? crap"-patterns
my @simplify_patterns = (
   qr{^"([^\\"'\ \t|&;<>()*?]+)"$}, # "simple" => simple
   qr{^(.*)[,\-]$},                 # strip off trailing , and -
);

sub on_sel_extend {
   my ($self) = @_;

   my ($row, $col) = $self->selection_mark;
   my $line = $self->line ($row);
   my $offset = $line->offset_of ($row, $col);
   my $text = $line->t;

   for my $regex (@mark_patterns) {
      while ($text =~ /$regex/g) {
         if ($-[1] <= $offset and $offset <= $+[1]) {
            my $ofs = $-[1];
            my $match = $1;

            for my $regex (@simplify_patterns) {
               if ($match =~ $regex) {
                  $match = $1;
                  $ofs += $-[1];
               }
            }

            $self->selection_beg ($line->coord_of ($ofs));
            $self->selection_end ($line->coord_of ($ofs + length $match));
            return 1;
         }
      }
   }

   ()
}
